#! /usr/bin/awk -f

BEGIN {
    # Every 2.5 minutes
    interval = 150
    tslen = 0
}

function output() {
    for (t in teams) {
        score = 0;
        for (c in points_by_cat) {
            score += points_by_cat_team[c, t] / points_by_cat[c];
        }
        if (score > maxscore) {
            maxscore = score
        }
        if (score > 0.009) {
            scores_by_team_time[t, lasttime] = score
        }
    }
    timestamps[tslen++] = lasttime
}

{
    time = $1
    team = $2
    cat = $3
    points = int($4)

    if (! start) {
        start = time
    }

    if (time > (outtime + interval)) {
        outtime = time
        output()
    }
    lasttime = time

    teams[team] = team
    points_by_cat[cat] += points
    points_by_cat_team[cat, team] += points
}

END {
    output()

    for (i = 1; i < tslen; i += 1) {
        time = timestamps[i]
        print time
        for (team in teams) {
            printf("  %s: %f\n", team, scores_by_team_time[team, time])
        }
    }
}
